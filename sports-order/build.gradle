plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.3'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'jacoco'
}

group = 'org.example'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}


repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    implementation 'org.hibernate.orm:hibernate-core:6.1.7.Final'
    implementation 'jakarta.persistence:jakarta.persistence-api:3.1.0'

    // data base
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.liquibase:liquibase-core'
    runtimeOnly 'org.postgresql:postgresql'
    implementation 'net.lbruun.springboot:preliquibase-spring-boot-starter:1.5.1'

    // security
    implementation 'org.springframework.boot:spring-boot-starter-security'

    // shedlock
    implementation 'net.javacrumbs.shedlock:shedlock-spring:5.12.0'
    implementation 'net.javacrumbs.shedlock:shedlock-provider-jdbc-template:5.12.0'

    // open api
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.4.0'
    implementation 'org.springdoc:springdoc-openapi-starter-common:2.4.0'

    // cloud
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
    implementation 'org.springframework.cloud:spring-cloud-starter-config'

    // lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:2023.0.3'
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

jacocoTestReport {
    dependsOn test
    reports {
        html.required = true
    }

    classDirectories.setFrom(files(classDirectories.files.collect {
        fileTree(dir: it).matching {
            exclude("**/configurations/**")
            exclude("**/util/**")
            exclude("**/SportsOrderApplication.class")
        }
    }))
}

test {
    finalizedBy jacocoTestReport
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.80
            }
        }
    }
}